##!
# @file
#
# @created smohan 20191105

sp {aileen*propose*interaction*message
    (state <s>    ^name aileen
                  ^message <m>
                  ^interaction <inter>)
    (<m>    -^processed true)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name interaction
             ^type incoming-message
             ^message <m>
             ^interaction <inter>)
          }

sp {aileen*propose*interaction*status
    (state <s>    ^name aileen
                  ^interaction <inter>)
    (<inter>    ^stack.segment <seg>)
    (<seg>    ^purpose <p>
              -^communicated)
    (<p>    ^completed <any>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name interaction
             ^type purpose-status
             ^interaction <inter>)
    }

sp {aileen*propose*interaction*processing-status
    (state <s>    ^name aileen
                  ^interaction <inter>
                  ^processing-status <ps>)
    (<ps>    -^processed true)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name interaction
             ^type new-processing-status
             ^interaction <inter>
             ^processing-status <ps>)
    }


sp {aileen*propose*comprehension
    (state <s>    ^name aileen
                  ^interaction.stack.segment <seg>)
    (<seg>    ^message <m>)
    -{(<seg>    ^grounding <g>)
      (<g>    ^complete true)}
    -{(<s>    ^processing-status <ps>)
      (<ps>    ^source comprehension)}
-->
    (<s>    ^operator <op> +)
    (<op>    ^name comprehension
             ^segment <seg>)
    }

sp {aileen*propose*verify
    (state <s>    ^name aileen
                  ^interaction.stack.segment <seg>)
    (<seg>    ^purpose <p>)
    (<p>    -^completed
             ^type verify)
    (<seg>    ^grounding <g>)
    (<g>    ^complete true)
    
-->
    (<s>    ^operator <op> +)
    (<op>    ^name verify
             ^segment <seg>)
    }

sp {aileen*propose*react
    (state <s>    ^name aileen
                  ^interaction.stack.segment <seg>)
    (<seg>    ^purpose <p>)
    (<p>    -^completed
             ^type react)
    (<seg>    ^grounding <g>)
    (<g>    ^complete true)
    
-->
    (<s>    ^operator <op> +)
    (<op>    ^name action
             ^segment <seg>)
    }


sp {aileen*propose*learn
    (state <s>    ^name aileen
                  ^interaction.stack.segment <seg>)
    (<seg>    ^information <info>
              ^purpose <p>)
    (<p>    -^completed
             ^type learn)
    -{(<s>    ^processing-status <ps>)
      (<ps>    ^source learning)}
-->
    (<s>    ^operator <op> +)
    (<op>    ^name learn
             ^segment <seg>)
    }


sp {aileen*propose*observe-demonstration
    (state <s>    ^name aileen
                  ^interaction.stack.segment <seg>)
    (<seg>    ^purpose <p>)
    (<p>    -^completed
            ^type observe-demonstration)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name observe-demonstration
             ^segment <seg>)
    }
              