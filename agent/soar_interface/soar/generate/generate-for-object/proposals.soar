##!
# @file
#
# @created smohan 20211102

sp {generate-for-object*propose*translate-to-facts
    (state <s>    ^name generate-for-object
                  ^object <obj>
                  -^facts <f>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name translate-to-facts
             ^object <obj>)
    }

sp {generate-for-object*propose*describe-subscene
    (state <s>    ^name generate-for-object
                  ^object <obj>
                  ^facts <f>
                  -^memory-lookup-status failed)
    (<obj> -^isa <con>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name describe-subscene
             ^facts <f>)
    }

sp {generate-for-object*propose*lookup-token
    (state <s>    ^name generate-for-object
                  ^object <obj>)
    -{(<s>    ^pair <p>)
      (<p>    ^concept <con>
              ^token <token>)}
    (<obj>    ^isa <con>)
-->
    (<s> ^operator <op> + =)
    (<op>    ^name lookup-token
             ^concept <con>)
    }

sp {generate-for-object*propose*generate-content
    (state <s>    ^name generate-for-object
                  ^object <obj>
                  -^content <sen>
                   ^pair <p>)
    (<obj>    ^id_string <id_string>)
    (<p>     ^concept <con>
             ^token <t>)
    (<obj>    ^isa <con>)
-->
    (<s>    ^operator <op> + < =)
    (<op>    ^name generate-content
             ^information.object <info>)
    (<info>    ^id <id_string>
               ^word <t>)
    }

sp {generate-for-object*propose*generate-content*elaborate
    (state <s>    ^name generate-for-object
                  ^object <obj>
                  -^content <sen>
                   ^pair <p>
                   ^operator <op> +)
    (<obj>    ^id_string <id_string>)
    (<p>     ^concept <con>
             ^token <t>)
    (<obj>    ^isa <con>
              ^id_string <id_string>)
    (<op>    ^name generate-content
             ^information.object <info>)
    (<info>    ^id <id_string>)
-->
    (<info>    ^word <t>)
    }

sp {generate-for-object*propose*complete-generate-object
    (state <s>    ^name generate-for-object
                  ^object <obj>
                  ^content <con>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name complete-generate-for-object
             ^content <con>)
    }

sp {generate-for-object*propose*mark-no-known-concept
    (state <s>    ^name generate-for-object
                  ^memory-lookup-status failed)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name mark-no-known-concept)
    }