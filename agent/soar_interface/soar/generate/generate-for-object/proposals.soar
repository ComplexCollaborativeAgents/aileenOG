##!
# @file
#
# @created smohan 20211102

sp {generate-for-object*propose*translate-to-facts
    (state <s>    ^name generate-for-object
                  ^objects.object <obj>
                 -^<obj> <f>)
-->
    (<s>    ^operator <op> + =)
    (<op>    ^name translate-to-facts
             ^object <obj>
             ^result <obj>)
    }

sp {generate-for-object*propose*describe-subscene
    (state <s>    ^name generate-for-object
                  ^objects.object <obj>
                  ^<obj> <f>
                  -^memory-lookup-status failed)
    (<obj> -^isa <con>)
-->
    (<s>    ^operator <op> + =)
    (<op>    ^name describe-subscene
             ^facts <f>)
    }

sp {generate-for-object*propose*lookup-token
    (state <s>    ^name generate-for-object
                  ^objects.object <obj>)
    -{(<s>    ^pair <p>)
      (<p>    ^concept <con>
              ^token <token>)}
    (<obj>    ^isa <con>)
-->
    (<s> ^operator <op> + =)
    (<op>    ^name lookup-token
             ^concept <con>)
    }

sp {generate-for-object*propose*complete-generate-for-object
    (state <s>    ^name generate-for-object
                  ^objects.object <obj>
                  ^pair <p>)
    (<p>     ^concept <con>
             ^token <t>)
    (<obj>    ^isa <con>)
-->
    (<s>    ^operator <op> + < =)
    (<op>    ^name complete-generate-for-object)
   }


sp {generate-for-object*propose*mark-no-known-concept
    (state <s>    ^name generate-for-object
                  ^memory-lookup-status failed)
-->
    (<s>    ^operator <op> + <)
    (<op>    ^name mark-no-known-concept)
    }