##!
# @file
#
# @created smohan 20191122


sp {learn-from-object*apply*send-instance-to-concept-memory
    (state <s>    ^name learn-from-object
                  ^operator <op>
                  ^epmem.present-id <pid>
                  ^_params.visual-concepts external
                  ^io.output-link <ol>)
    (<op>    ^name send-instance-to-concept-memory
             ^concept <lti>
             ^object <obj>)
    (<lti>    ^gpool <gpool>
              ^concept <con>)
    (<obj>    ^id_string <id_string>)
-->
    (<ol>    ^concept-memory <cm>)
    (<cm>    ^store <si>)
    (<si>    ^gpool <gpool>
             ^concept <con>
             ^facts <facts>
             ^context <pid>)
    (<facts>    ^fact <f>)
    (<f>    ^lfirst isa
            ^lsecond <id_string>
            ^lthird <con>)
      ### HACK:: elaborate the object with this symbol. Basically, the inference leads to a hypothesis that this has to be true. This labeling is also stored in memory.
    (<obj>    ^isa <con>)
    }

sp {learn-from-object*apply*send-instance-to-concept-memory*object-attributes*color*shape
    (state <s>    ^name learn-from-object
                  ^operator <op>
                  ^_params.visual-concepts external
                  ^io.output-link <ol>)
                  #^topstate._params.relevant-percept-set <set>)
    (<op>    ^name send-instance-to-concept-memory
             ^concept <lti>
             ^object <obj>)
    (<ol>    ^concept-memory <cm>)
    (<cm>    ^store <si>)
    (<si>    ^facts <facts>)
    #(<set>    ^type { <type> << color shape >>})
    (<obj>    ^<< color shape >> <symbol>
              ^id_string <id_string>)
-->
    (<facts>    ^fact <f>)
    (<f>    ^lfirst isa
            ^lsecond <id_string>
            ^lthird <symbol>)
    }

sp {learn-from-object*apply*send-instance-to-concept-memory*object-attributes*size
    (state <s>    ^name learn-from-object
                  ^operator <op>
                  ^_params.visual-concepts external
                  ^io.output-link <ol>)
                  #^topstate._params.relevant-percept-set <set>)
    (<op>    ^name send-instance-to-concept-memory
             ^concept <lti>
             ^object <obj>)
    (<ol>    ^concept-memory <cm>)
    (<cm>    ^store <si>)
    (<si>    ^facts <facts>)
    #(<set>    ^type {<type> << size >>})
    (<obj>    ^size <symbol>
              ^id_string <id_string>)
-->
    (<facts>    ^fact <f>)
    (<f>    ^lfirst size
            ^lsecond <id_string>
            ^lthird <symbol>)
    }

sp {learn-from-object*apply*send-instance*status
    (state <s>    ^name learn-from-object
                  ^operator <op> 
                  ^io.output-link.concept-memory.store.gpool)
    (<op>    ^name send-instance-to-concept-memory
             ^object <obj>)
-->
    (<s>    ^stored <obj>)
    }