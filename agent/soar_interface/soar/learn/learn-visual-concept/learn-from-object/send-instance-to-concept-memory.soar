##!
# @file
#
# @created smohan 20191122


sp {learn-from-object*apply*send-instance-to-concept-memory
    (state <s>    ^name learn-from-object
                  ^operator <op>
                  ^epmem.present-id <pid>
                  ^_params.visual-concepts external
                  ^io.output-link <ol>)
    (<op>    ^name send-instance-to-concept-memory
             ^concept <lti>
             ^object <obj>)
    (<lti>    ^gpool <gpool>
              ^concept <con>)
    (<obj>    ^id_string <id_string>)
-->
    (<ol>    ^concept-memory <cm>)
    (<cm>    ^store <si>)
    (<si>    ^gpool <gpool>
             ^facts <facts>
             ^context <pid>)
    (<facts>    ^fact <f>)
    (<f>    ^lfirst isa
            ^lsecond <id_string>
            ^lthird <con>)
    }

sp {learn-from-object*apply*send-instance-to-concept-memory*object-attributes
    (state <s>    ^name learn-from-object
                  ^operator <op>
                  ^_params.visual-concepts external
                  ^io.output-link <ol>
                  ^topstate._params.relevant-percept-set <set>)
    (<op>    ^name send-instance-to-concept-memory
             ^concept <lti>
             ^object <obj>)
    (<ol>    ^concept-memory <cm>)
    (<cm>    ^store <si>)
    (<si>    ^facts <facts>)
    (<set>    ^type <type>)
    (<obj>    ^<type> <symbol>
              ^id_string <id_string>)
-->
    (<facts>    ^fact <f>)
    (<f>    ^lfirst isa
            ^lsecond <id_string>
            ^lthird <symbol>)
    }

sp {learn-from-object*apply*send-instance*status
    (state <s>    ^name learn-from-object
                  ^operator <op> 
                  ^io.output-link.concept-memory.store.gpool)
    (<op>    ^name send-instance-to-concept-memory
             ^object <obj>)
-->
    (<s>    ^stored <obj>)
    }