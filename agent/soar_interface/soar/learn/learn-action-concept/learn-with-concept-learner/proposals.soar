##!
# @file
#
# @created smohan 20200118
sp {learn-action-with-concept-learner*propose*store-new-concept
    (state <s>    ^name learn-action-with-concept-learner
                  ^concept-structure <con-s>)
    (<con-s>    -^gpool)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name store-new-concept
             ^concept-structure <con-s>)
    }


sp {learn-action-with-concept-learner*propose*gather-action-trace
    (state <s>    ^name learn-action-with-concept-learner
                  ^concept-structure <struct>
                  -^trace <t>)
    (<struct>    ^gpool)
-->
    (<s>    ^operator <op> + =)
    (<op>    ^name gather-trace
             ^concept-structure <struct>)
    }

sp {learn-action-with-concept-learner*propose*augment-trace-with-action
    (state <s>    ^name learn-action-with-concept-learner
                  ^trace <t>
                  ^concept-structure <lti>
                  -^augmented-with <con>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name augment-trace-with-action
             ^trace <t>
             ^concept-structure <lti>)
    }


sp {learn-action-with-concept-learner*propose*send-instance-to-concept-memory
    (state <s>    ^name learn-action-with-concept-learner
                  ^trace <t>
                  ^concept-structure <con>
                  ^augmented-with <con>
                  -^stored <t>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name send-instance-to-concept-memory
             ^concept <con>
             ^trace <t>)
    }

sp {learn-action-with-concept-learner*propose*complete-learn-action-with-concept-learner
    (state <s>    ^name learn-action-with-concept-learner
                  ^concept-structure <lti>
                  ^trace <t>
                  ^stored <t>
                  ^augmented-with <lti>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name complete-learn-action-with-concept-learner)
    }

