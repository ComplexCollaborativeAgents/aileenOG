##!
# @file
#
# @created smohan 20200118
sp {learn-action-with-concept-learner*propose*initialize-new-concept
    (state <s>    ^name learn-action-with-concept-learner
                  -^concept-structure
                   ^nl <tag>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name initialize-new-concept
             ^nl <tag>)
    }

sp {learn-action-with-concept-learner*propose*store-new-concept
    (state <s>    ^name learn-action-with-concept-learner
                  ^concept-structure <con-s>)
    (<con-s>    -^gpool)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name store-new-concept
             ^concept-structure <con-s>)
    }

sp {learn-action-with-concept-learner*propose*compile-action-trace
    (state <s>    ^name learn-action-with-concept-learner
                  ^concept-structure <con>
                  -^trace <t>)
    (<con>    ^gpool <g>)
-->
    (<s>    ^operator <op> + =)
    (<op>    ^name compile-action-trace
             ^episode <res>)
    }
             
sp {learn-action-with-concept-learner*propose*augment-trace-with-action
    (state <s>    ^name learn-action-with-concept-learner
                  ^trace <t>
                  ^concept-structure <lti>
                  -^augmented-with <con>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name augment-trace-with-action
             ^trace <t>
             ^concept-structure <lti>)
    }

sp {learn-action-with-concept-learner*propose*translate-to-facts
    (state <s>    ^name learn-action-with-concept-learner
                  ^trace <t>
                  -^facts <f>)
-->
    (<s>    ^operator <op> + <)
    (<op>    ^name translate-to-facts
             ^trace <t>)
    }

sp {learn-action-with-concept-learner*propose*store-in-concept-memory
    (state <s>    ^name learn-action-with-concept-learner
                  ^facts <f>
                  ^concept-structure.concept <con>
                  -^stored <con>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name store-in-concept-memory
             ^facts <f>
             ^concept <con>)
    }

sp {learn-action-with-concept-learner*propose*complete-learn-action-with-concept-learner
    (state <s>    ^name learn-action-with-concept-learner
                  ^concept-structure.concept <con>
                  ^stored <con>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name complete-learn-action-with-concept-learner)
    }

