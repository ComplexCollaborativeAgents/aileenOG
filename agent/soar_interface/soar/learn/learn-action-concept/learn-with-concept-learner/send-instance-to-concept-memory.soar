##!
# @file
#
# @created smohan 20200220


sp {learn-action-with-concept-learner*apply*send-instance-to-concept-memory
    (state <s>    ^name learn-action-with-concept-learner
                  ^operator <op>
                  ^epmem.present-id <pid>
                  ^io.output-link <ol>)
    (<op>    ^name send-instance-to-concept-memory
             ^concept <lti> 
             ^trace <t>)
    (<lti>    ^gpool <gpool>
              ^concept <con>)
-->
    (<ol>    ^concept-memory <cm>)
    (<cm>    ^store <si>)
    (<si>    ^concept <con>
             ^context <pid>
             ^facts <fact>)
    }

sp {learn-action-with-concept-learner*appl*send-instance-to-concept-memory*action
    (state <s>    ^name learn-action-with-concept-learner
                  ^operator <op>
                  ^io.output-link.concept-memory.store <si>)
    (<si>    ^facts <facts>)
    (<op>    ^trace.action <act>)
    (<act>    ^internal <name>
              ^first.id_string <obj-name>)
-->
    (<facts>    ^fact <f>)
    (<f>    ^lfirst <name>
            ^lsecond <obj-name>)
    }
    
sp {learn-action-with-concept-learner*apply*send-instance-to-concept-memory*relations
    (state <s>    ^name learn-action-with-concept-learner
                  ^operator <op>
                  ^io.output-link.concept-memory.store <si>)
    (<si>    ^facts <facts>)
    (<op>    ^trace.state-record <sr>)
    (<sr>    ^label <lab>)
    (<sr>    ^relations.relation <rel>)
    (<rel>    ^<< rcc8 cardir >> <val>
              ^root-object.id_string <fid>
              ^target-object.id_string <sid>)
-->
    (<facts>    ^fact <f>)
    (<f>    ^lfirst holdsIn
            ^lsecond <lab>
            ^lthird <srel>)
    (<srel>    ^lfirst <val>
               ^lsecond <fid>
               ^lthird <sid>)
    }

sp {learn-action-with-concept-learner*apply*send-instance-to-concept-memory*relations*objects
    (state <s>    ^name learn-action-with-concept-learner
                  ^operator <op>
                  ^io.output-link.concept-memory.store <si>
                  ^topstate._params.relevant-percept-set <set>)
    (<set>    ^type <type>)
    (<si>    ^facts <facts>)
    (<op>    ^trace.state-record <sr>)
    (<sr>    ^label <lab>)
    (<sr>    ^objects.object <obj>)
    (<obj>    ^id_string <id_string>
              ^<type> <symbol>)
    (<si>    ^facts <fact>)
-->
    (<facts>    ^fact <f>)
    (<f>    ^lfirst holdsIn
            ^lsecond <lab>
            ^lthird <objf>)
    (<objf>    ^lfirst isa
               ^lsecond <id_string>
               ^lthird <symbol>)
    }

sp {learn-action-with-concept-learner*apply*send-instance-to-concept-memory*status
    (state <s>    ^name learn-action-with-concept-learner
                  ^operator <op>)
    (<op>    ^name send-instance-to-concept-memory
             ^trace <t>)
-->
    (<s>    ^stored <t>)
    }