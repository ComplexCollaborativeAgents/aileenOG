##!
# @file
#
# @created smohan 20191210

sp {learn-spatial-concept*apply*complete-learn-spatial-concept
    (state <s>    ^name learn-spatial-concept
                  ^superstate.segment <seg>
                  ^information <info>
                  ^operator.name complete-learn-spatial-concept)
-->
    (<seg>    ^learned <info>)
    }

sp {learn-spatial-concept*apply*complete-learn-spatial-concept*grounding*clear*learn-until-correct
    (state <s>    ^name learn-spatial-concept
                  ^information.grounding <g>
                  ^operator.name complete-learn-spatial-concept
                  ^topstate._params.interaction.recomprehend true)
    (<g>    ^complete true)
-->
    (<g>    ^complete true -)
    }

sp {learn-spatial-concept*apply*complete-learn-spatial-concept*grounding*write-relation-to-world
    (state <s>    ^name learn-spatial-concept
                  ^operator.name complete-learn-spatial-concept
                  ^world.relations <wrels>
                  ^selected-scene <scene>)
    (<scene>     ^relations.relation <rel>)
    (<rel>     ^spatial-concept <con>)
-->
    (<wrels> ^relation <rel>)
    }

sp {learn-spatial-concept*clear*concept-memory*topstate
    (state <s>    ^name learn-spatial-concept
                  ^topstate <ts>
                  ^operator.name complete-learn-spatial-concept)
    (<ts>    ^concept-memory <cm>)
-->
    (<ts>    ^concept-memory <cm> -)
    }