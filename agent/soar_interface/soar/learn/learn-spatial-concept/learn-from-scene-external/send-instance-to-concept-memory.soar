##!
# @file
#
# @created smohan 20191208

sp {learn-from-scene-external*apply*send-instance-to-concept-memory
    (state <s>    ^name learn-from-scene-external
                  ^operator <op>
                  ^epmem.present-id <pid>
                  ^io.output-link <ol>)
    (<op>    ^name send-instance-to-concept-memory
             ^concept <lti>
             ^scene.relations <rels>)
    (<lti>    ^gpool <gpool>
              ^concept <con>)
-->
    (<ol>    ^concept-memory <cm>)
    (<cm>    ^store <si>)
    (<si>    ^concept <con>
             ^context <pid>
             ^facts <fact>)
    }

sp {learn-from-scene-external*apply*send-instance-to-concept-memory*facts*new-rel
    (state <s>    ^name learn-from-scene-external
                  ^operator <op>
                  ^io.output-link.concept-memory.store <si>)
    (<si>    ^facts <facts>)
    (<op>    ^scene.relations.relation <rel>)
    (<rel>    ^internal <int>
              ^first.id_string <fid>
              ^second.id_string <sid>)
-->
    (<facts> ^fact <f>)
    (<f>   ^lfirst <int>
           ^lsecond <fid>
           ^lthird <sid>)}

sp {learn-from-scene-external*apply*send-instance-to-concept-memory*facts*objects
    (state <s>    ^name learn-from-scene-external
                  ^operator <op>
                  ^io.output-link.concept-memory.store <si>
                  ^topstate._params.relevant-percept-set <set>)
    (<set>    ^type <type>)
    (<si>    ^facts <facts>)
    (<op>    ^scene.objects.object <obj>)
    (<obj>    ^id_string <id_string>
              ^<type> <symbol>)
    (<si>    ^facts <facts>)
-->
    (<facts> ^fact <f>)
    (<f>    ^lfirst isa
            ^lsecond <id_string>
            ^lthird <symbol>)
    }


sp {learn-from-scene-external*apply*send-instance-to-concept-memory*spatial
    (state <s>    ^name learn-from-scene-external
                  ^operator <op>
                  ^io.output-link.concept-memory.store <si>)
    (<si>    ^facts <facts>)
    (<op>    ^scene.relations.relation <rel>)
    (<rel>    ^<< rcc8 cardir >> <val>
              ^root.id_string <fid>
              ^target.id_string <sid>)
-->
    (<facts> ^fact <f>)
    (<f>   ^lfirst <val>
           ^lsecond <fid>
           ^lthird <sid>)}


   
sp {learn-from-scene-external*apply*send-instance-to-concept-memory*status
    (state <s>    ^name learn-from-scene-external
                  ^operator <op>
                  ^io.output-link.concept-memory.store.facts.fact <f>)
    (<op>    ^name send-instance-to-concept-memory
             ^scene <scene>)
-->
    (<s>    ^stored <scene>)
    }
                  