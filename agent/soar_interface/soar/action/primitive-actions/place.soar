##!
# @file
#
# @created smohan 20200513

sp {action*propose*place
    (state <s>    ^name action
                  ^holdsInHand <any>
                  ^desired <d>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name op_place
             ^object <any>
             ^location <loc>)
    }

## SM 05/15/2020: This model is wrong. It assumes that all relations containing that object can be achieved by a single place action.
## This assumptions is very likely false.

sp {action*elaborate*place
    (state <s>    ^name action
                  ^operator <op> +
                  ^desired <d>)
    (<op>    ^name op_place
             ^object <obj1>)
    (<d>    ^relations.relation <rel>)
    (<rel>    ^<attr> <sym>
              ^<< target-object root-object >> <obj1>)
-->
    (<op>    ^relation <rel>)
    }

sp {action*apply*place*target-object
    (state <s>    ^name action
                  ^operator <op>
                  ^io.output-link <ol>)
    (<op>    ^name op_place
             ^object <obj1>)
    (<obj1>    ^id <id>
               ^size_bb.xsize <xsize>
               ^size_bb.zsize <zsize>)
-->
    (<ol>    ^action <act>)
    (<act>    ^name place
              ^target <t>)
    (<t>    ^id <id>
            ^xsize <xsize>
            ^zsize <zsize>)
    }

sp {action*apply*place*reference-object
    (state <s>    ^name action
                  ^operator <op> 
                  ^io.output-link <ol>)
    (<op>    ^name op_place
             ^object <obj1>
             ^relation <rel>)
    (<rel>    ^<< target-object root-object >> <obj1>
              ^<< target-object root-object >> <obj2> {<> <obj1> <obj2>})
    (<ol>    ^action <act>)
    (<obj2>    ^position.x <xpos>
               ^position.y <ypos>
               ^position.z <zpos>
               ^size_bb.xsize <xsize>
               ^size_bb.zsize <zsize>
               ^id <id>)
-->
    (<act>    ^reference <ref>)
    (<ref>    ^id <id>
              ^xsize <xsize>
              ^zsize <zsize>
              ^xpos <xpos>
              ^ypos <ypos>
              ^zpos <zpos>)
    }

sp {action*apply*place*relation
    (state <s>    ^name action
                  ^operator <op>
                  ^io.output-link.action <act>
                  ^topstate._params.spatial-predicates.type <spred>)
    (<op>    ^name op_place
             ^relation <rel>
             ^object.id <oid>)
    (<rel>    ^target-object.id <rid>
              ^root-object.id <oid>
              ^<spred> <sym>)
-->
    (<act>    ^relation <ri>)
    (<ri>    ^target <oid>
             ^root <rid>
             ^qsr <sym>)}



sp {action*model*place
    (state <s>    ^name action
                  ^operator <op>
                  ^relations <wrels>
                  -^io)
    (<op>    ^name op_place
             ^relation <rel>)
-->
    (<wrels>    ^relation <rel>)
    }
    