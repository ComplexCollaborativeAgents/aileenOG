##!
# @file
#
# @created smohan 20191220

sp {action*elaborate*state
    (state <s>    ^name action
                  ^superstate.operator <sop>)
    (<sop>    ^name action
              ^segment <seg>)
-->
    (<s>    ^segment <seg>)
    }

sp {action*elaborate*state*action
    (state <s>    ^name action
                  ^segment <seg>)
    (<seg>    ^grounding <g>)
    (<g>    ^action <act>)
-->
    (<s>    ^action <act>
            ^grounding <g>)
    }

sp {action*elaborate*state*problemspace
    (state <s>    ^name action)
-->
    (<s>    ^problem-space <ps>)
    (<ps>    ^name generate-instruction
            ^default-state-copy yes
            ^two-level-attributes world
            ^one-level-attributes desired)
    }

sp {action*complete*action
    (state <s>    ^name action
                  ^action <act>
                  ^complete <act>
                  ^segment.purpose <p>)
    (<p>    ^type react)
-->
    (<p>    ^completed true
            ^status success)
    }

sp {action*elaborate*goal*test
    (state <s>    ^name action
                  ^world <w>
                  ^desired <d>)
    (<d>    ^holdsInHand <obj>)
    (<w>    ^holdsInHand <obj>)
-->
    (<s>    ^success <d>)
    }