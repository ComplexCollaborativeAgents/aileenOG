##!
# @file
#
# @created smohan 20200416

sp {determine-next-state*propose*select-scene-subset
    (state <s>    ^name determine-next-state
                  -^scene <scene>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name select-scene-subset)
    }

sp {determine-next-state*propose*collect-facts
    (state <s>    ^name determine-next-state
                 -^facts <facts>
                  ^scene <scene>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name translate-to-facts
             ^trace <scene>)
    }

sp {determine-next-state*propose*project-concept
    (state <s>    ^name determine-next-state
                  ^concept <con>
                  ^facts <facts>
                  -^retrieved)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name project-concept
             ^concept <con>
             ^facts <facts>)
    }

sp {determine-next-state*propose*generate-next-state
    (state <s>    ^name determine-next-state
                  ^concept <con>
                  ^facts <facts>
                  ^retrieved {<ret> <> failure}
                  -^projected-world <pw>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name generate-next-state
             ^projections <ret>)
    }

sp {determine-next-state*propose*complete-determine-next-state
    (state <s>    ^name determine-next-state
                  ^projected-world <pw>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name complete-determine-next-state)
    }