##!
# @file
#
# @created smohan 20191216

sp {verify-relation*elaborate*relation
    (state <s>    ^name verify-relation
                  ^superstate.operator <sop>)
    (<sop>    ^name verify-relation
              ^relation <rel>)
-->
    (<s>    ^g-relation <rel>)
    }

sp {verify-relation*elaborate*pairs
    (state <s>    ^name verify-relation
                  ^g-relation.relation <rel>)
    (<rel>    ^root.object <robj>
              ^target.object <tobj>
              ^primitives <p>)
-->
    (<s>    ^pair <pair>)
    (<pair>    ^root <robj>
            ^target <tobj>
            ^primitives <p>)
    }

sp {verify-relation*elaborate*world*match
    (state <s>    ^name verify-relation
                  ^world.relations <rels>
                  ^pair <p>
                  ^topstate._params.spatial-predicates.type <spred>)
    (<rels>    ^relation <rel>)
    (<rel>    ^{<type> <spred>} <qsr>
              ^root-object <robj>
              ^target-object <tobj>)
    (<p>    ^root <robj>
            ^target <tobj>
            ^primitives.<type> <qsr>)
-->
    (<p>    ^matched <qsr>)
}

sp {verify-relation*report*verified
    (state <s>    ^name verify-relation
                  ^pair <p>
                  ^g-relation <rel>
                  ^superstate <ss>
                  ^topstate._params.spatial-predicates.type <spred>)
    -{(<p>    ^primitives.{<type> <spred>} <qsr>)
      (<p>   -^matched <qsr>)}
-->
    (<ss>    ^verified <rel>)
    }


sp {verify-relation*report*success
    (state <s>    ^name verify-relation
                  ^pair <p>
                  ^superstate <ss>
                  ^topstate._params.spatial-predicates.type <spred>)
    -{(<p>    ^primitives.{<type> <spred>} <qsr>)
      (<p>   -^matched <qsr>)}
-->
    (<ss>    ^operator <op> + !)
    (<op>    ^name report-success)
    }


    
                  