##!
# @file
#
# @created smohan 20191114



## verification is successful if there are one or more objects that the object reference can be grounded into
sp {verify*propose*report-success*object
    (state <s>    ^name verify
                  ^segment <seg>)
    (<seg>    ^parses.parse.obj-ref <oref>
              ^grounding <g>)
    (<g>    ^entity <g-entity>)
    (<g-entity>    ^_metadata.description <oref>
                   ^object <obj>)
-->
    (<s>    ^operator <op> + =)
    (<op>    ^name report-success)
    }

sp {verify*propose*report-failure*object
    (state <s>    ^name verify
                  ^segment <seg>)
    (<seg>    ^parses.parse.obj-ref <oref>
              ^grounding <g>)
    -{(<g>    ^entity <g-entity>)
    (<g-entity>    ^_metadata.description <oref>
                   ^object <obj>)}
-->
    (<s>    ^operator <op> +)
    (<op>    ^name report-failure)
    }

sp {verify*propose*report-success*relation
    (state <s>    ^name verify
                  ^segment <seg>)
    (<seg>    ^parses.parse.rel-ref.tag <rref>
              ^grounding <g>)
    (<g>    ^relation <g-entity>)
    (<g-entity>    ^_metadata.description <rref>
                   ^relation <rel>)
-->
    (<s>    ^operator <op> + >!)
    (<op>    ^name report-success)
    }
                  

sp {verify*propose*verify-relation-refence
    (state <s>    ^name verify
                  ^grounding.relation <rel>
                  -^verified <rel>)
-->
    (<s>    ^operator <op> + > =)
    (<op>    ^name verify-relation
             ^relation <rel>)
    }

sp {verify*propose*verify-action
    (state <s>    ^name verify
                  ^grounding.action <act>
                  -^verified <act>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name verify-action
             ^action <act>)
    }

sp {verify*propose*report-success*action
    (state <s>    ^name verify
                  ^grounding.action <act>
                  ^verified <act>)
-->
    (<s>    ^operator <op> + >!)
    (<op>    ^name report-success)
    }
             