##!
# @file
#
# @created smohan 20200311


sp {verify-action*propose*compile-action-trace
    (state <s>    ^name verify-action
                  -^trace <t>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name compile-action-trace)
    }

sp {verify-action*propse*translate-to-facts
    (state <s>    ^name verify-action
                  ^trace <t>
                  -^facts <f>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name translate-to-facts
             ^trace <t>)
    }

sp {verify-action*propose*query-concept-memory
    (state <s>    ^name verify-action
                  ^facts <f>
                  ^pattern <p>
                  -^retrieved <any>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name query-concept-memory
             ^facts <f>
             ^pattern <p>)
    }

sp {verify-action*propose*mark-overspecific-action-concept
    (state <s>    ^name verify-action
                  ^retrieved <ret>)
    (<ret>    -^match)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name mark-overspecific-action-concept)
    }

sp {verify-action*propose*report-succes
    (state <s>    ^name verify-action
                  ^retrieved.match <any>
                 )
-->
    (<s>    ^operator <op> + !)
    (<op>    ^name report-success)
    }

sp {verify-action*superstate*propose*report-succes
    (state <s>    ^name verify-action
                  ^operator.name report-success
                  ^superstate <ss>
                  ^action <act>)
-->
    (<ss>    ^verified <act>)
    }