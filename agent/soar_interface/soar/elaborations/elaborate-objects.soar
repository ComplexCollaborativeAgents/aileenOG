##!
# @file
#
# @created smohan 20191115


sp {aileen*elaborate*objects*copy
    :o-support
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects <wobjs>)
    (<il>    ^world.objects.object <obj>)
    (<obj>    ^color <color>
              ^shape <shape>
              ^held <held>
              ^id <id>
              ^id_uuid <id-uuid>
              ^id_string <id-string>)
   -{(<wobjs>    ^object <wobj>)
    (<wobj>    ^id <id>)}
-->
    (<wobjs>    ^object <obj-new>)
    (<obj-new>    ^color <color>
                  ^shape <shape>
                  ^held <held>
                  ^id <id>
                  ^id_uuid <id-uuid>
                  ^id_string <id-string>)
    }

sp {aileen*elaborate*objects*remove
    :o-support
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects <wobjs>)
    -{(<il>    ^world.objects.object <obj>)
    (<obj>    ^id <id>)}
    (<wobjs>    ^object <wobj>)
    (<wobj>    ^id <id>)
-->
    (<wobjs>    ^object  <wobj> -)
    }

sp {aileen*elaborate*objects*update*held
    :o-support
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects.object <wobj>)
    (<il>    ^world.objects.object <obj>)
    (<obj>    ^id <id>
              ^held <held>)
    (<wobj>    ^id <id>
               ^held {<nheld> <> <held>})
-->
    (<wobj>    ^held <nheld> -
               ^held <held>)
    }


sp {aileen*elaborate*objects*update
    :o-support
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects <wobjs>)
    (<il>    ^world.objects.object <obj>)
    (<obj>    ^id <id>
              ^position <ipose>)
    (<wobjs>    ^object <wobj>)
    (<wobj>    ^id <id>
                ^position <wpose>)
-->
    (<wobj>    ^position <wpose> -
               ^position <ipose>)
    }

sp {aileen*elaborate*add*qsrs
    :o-support
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world <w>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<il>    ^world.qsrs.qsr <qsr>)
    (<qsr>    ^root <root-num>
              ^target <target-num>
              ^{<< cardir rcc8 >> <type>} <val>)
    (<objs>    ^object <root-obj>
               ^object <target-obj>)
    (<root-obj>    ^id <root-num>)
    (<target-obj>    ^id <target-num>)
-->
    (<rels>    ^relation <rel-new>)
    (<rel-new>    ^root-object <root-obj>
                  ^target-object <target-obj>
                  ^<type> <val>)
    }

sp {aileen*elaborate*remove*qsrs*no-root-object
    :o-support
    (state <s>    ^name aileen
                  ^world <w>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^<< cardir rcc8 >> <val>)
    (<objs>    -^object <root-obj>)
-->
    (<rels>    ^relation <rel> -)
    }

sp {aileen*elaborate*remove*qsrs*no-target-object
    :o-support
    (state <s>    ^name aileen
                  ^world <w>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^<< cardir rcc8 >> <val>)
    (<objs>    -^object <target-obj>)
-->
    (<rels>    ^relation <rel> -)
    }

sp {aileen*elaborate*remove*qsrs*different-qsr
    :o-support
    (state <s>    ^name aileen
                  ^world <w>
                  ^io.input-link.world.qsrs.qsr <irel>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^{<< cardir rcc8 >> <type>} <val>)
    (<objs>    ^object <target-obj> <root-obj>)
    (<irel>    ^root <root-num>
               ^target <target-num>
               ^<type> {<nval> <> <val>})
    (<root-obj>    ^id <root-num>)
    (<target-obj>    ^id <target-num>)
-->
    (<rels>    ^relation <rel> -)
    }

sp {aileen*elaborate*remove*qsrs*no-qsr
    :o-support
    (state <s>    ^name aileen
                  ^world <w>
                  ^io.input-link.world.qsrs <qsrs>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^{<< cardir rcc8 >> <type>} <val>)
    (<objs>    ^object <target-obj> <root-obj>)
    -{(<qsrs>  ^qsr <q>)
       (<q>    ^root <root-num>
               ^target <target-num>)}
-->
    (<rels>    ^relation <rel> -)
    }
      
              
