##!
# @file
#
# @created smohan 20191115


sp {aileen*elaborate*objects*copy
    :o-support
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects <wobjs>)
    (<il>    ^world.objects.object <obj>)
    (<obj>    ^color <color>
              ^shape <shape>
              ^held <held>
              ^id <id>
              ^id_uuid <id-uuid>
              ^id_string <id-string>
              ^size <sz>)
   -{(<wobjs>    ^object <wobj>)
    (<wobj>    ^id <id>)}
-->
    (<wobjs>    ^object <obj-new>)
    (<obj-new>    ^color <color>
                  ^shape <shape>
                  ^held <held>
                  ^id <id>
                  ^id_uuid <id-uuid>
                  ^id_string <id-string>
                  ^size <sz>)
    }

sp {aileen*elaborate*objects*remove
    :o-support
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects <wobjs>)
    -{(<il>    ^world.objects.object <obj>)
    (<obj>    ^id <id>)}
    (<wobjs>    ^object <wobj>)
    (<wobj>    ^id <id>)
-->
    (<wobjs>    ^object  <wobj> -)
    }

sp {aileen*elaborate*objects*position
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects.object <wobj>)
    (<il>    ^world.objects.object <obj>)
    (<obj>    ^id <id>
              ^position <p>)
    (<wobj>    ^id <id>)
-->
    (<wobj>    ^position <p>)
    }

sp {aileen*elaborate*objects*size_bb
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects.object <wobj>)
    (<il>    ^world.objects.object <obj>)
    (<obj>    ^id <id>
              ^size_bb <p>)
    (<wobj>    ^id <id>)
-->
    (<wobj>    ^size_bb <p>)
    }

sp {aileen*elaborate*objects*update*held
    :o-support
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects.object <wobj>)
    (<il>    ^world.objects.object <obj>)
    (<obj>    ^id <id>
              ^held <held>)
    (<wobj>    ^id <id>
               ^held {<nheld> <> <held>})
-->
    (<wobj>    ^held <nheld> -
               ^held <held>)
    }

sp {aileen*elaborate*object*holdInHand
    (state <s>    ^name aileen
                  ^world <w>)
    (<w>    ^objects.object <obj>)
    (<obj>  ^held true)
-->
    (<w>    ^holdsInHand <obj>)
    }

sp {aileen*elaborate*add*qsrs
    :o-support
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world <w>
                  ^topstate._params.spatial-predicates.type <spred>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<il>    ^world.qsrs.qsr <qsr>)
    (<qsr>    ^root <root-num>
              ^target <target-num>
              ^<spred> <val>)
    (<objs>    ^object <root-obj>
               ^object <target-obj>)
    (<root-obj>    ^id <root-num>)
    (<target-obj>    ^id <target-num>)
    -{(<rels>    ^relation <rel>)
      (<rel>    ^root-object <root-obj>
                ^target-object <target-obj>
                ^<type> <val>)}
-->
    (<rels>    ^relation <rel-new>)
    (<rel-new>    ^root-object <root-obj>
                  ^target-object <target-obj>
                  ^<spred> <val>)
    }

sp {aileen*elaborate*remove*qsrs*no-root-object
    :o-support
    (state <s>    ^name aileen
                  ^world <w>
                  ^topstate._params.spatial-predicates.type <spred>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^<spred> <val>)
    (<objs>    -^object <root-obj>)
-->
    (<rels>    ^relation <rel> -)
    }

sp {aileen*elaborate*remove*qsrs*no-target-object
    :o-support
    (state <s>    ^name aileen
                  ^world <w>
                  ^topstate._params.spatial-predicates.type <spred>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^<spred> <val>)
    (<objs>    -^object <target-obj>)
-->
    (<rels>    ^relation <rel> -)
    }

sp {aileen*elaborate*remove*qsrs*different-qsr
    :o-support
    (state <s>    ^name aileen
                  ^world <w>
                  ^io.input-link.world.qsrs.qsr <irel>
                  ^topstate._params.spatial-predicates.type <spred>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^<spred> <val>)
    (<objs>    ^object <target-obj> <root-obj>)
    (<irel>    ^root <root-num>
               ^target <target-num>
               ^<spred> {<nval> <> <val>})
    (<root-obj>    ^id <root-num>)
    (<target-obj>    ^id <target-num>)
-->
    (<rels>    ^relation <rel> -)
    }

sp {aileen*elaborate*remove*qsrs*no-qsr
    :o-support
    (state <s>    ^name aileen
                  ^world <w>
                  ^io.input-link.world.qsrs <qsrs>
                  ^topstate._params.spatial-predicates.type <spred>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^<spred> <val>)
    (<objs>    ^object <target-obj> <root-obj>)
    -{(<qsrs>  ^qsr <q>)
       (<q>    ^root <root-num>
               ^target <target-num>)}
-->
    (<rels>    ^relation <rel> -)
    }

#### CSR

sp {aileen*elaborate*add*csrs
    :o-support
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world <w>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<il>    ^world.csrs.csr <qsr>)
    (<qsr>    ^root <root-num>
              ^target <target-num>
              ^{<< distance >> <type>} <val>)
    (<objs>    ^object <root-obj>
               ^object <target-obj>)
    (<root-obj>    ^id <root-num>)
    (<target-obj>    ^id <target-num>)
    -{(<rels>    ^relation <rel>)
      (<rel>    ^root-object <root-obj>
                ^target-object <target-obj>
                ^<type> <val>)}
-->
    (<rels>    ^relation <rel-new>)
    (<rel-new>    ^root-object <root-obj>
                  ^target-object <target-obj>
                  ^<type> <val>)
    }


sp {aileen*elaborate*remove*csrs*different-csr
    :o-support
    (state <s>    ^name aileen
                  ^world <w>
                  ^io.input-link.world.csrs.csr <irel>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^{<< distance >> <type>} <val>)
    (<objs>    ^object <target-obj> <root-obj>)
    (<irel>    ^root <root-num>
               ^target <target-num>
               ^<type> {<nval> <> <val>})
    (<root-obj>    ^id <root-num>)
    (<target-obj>    ^id <target-num>)
-->
    (<rels>    ^relation <rel> -)
    }

sp {aileen*elaborate*remove*csrs*no-csr
    :o-support
    (state <s>    ^name aileen
                  ^world <w>
                  ^io.input-link.world.csrs <qsrs>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^{<< distance >> <type>} <val>)
    (<objs>    ^object <target-obj> <root-obj>)
    -{(<qsrs>  ^csr <q>)
       (<q>    ^root <root-num>
               ^target <target-num>)}
-->
    (<rels>    ^relation <rel> -)
    }

sp {aileen*elaborate*remove*csrs*no-root-object
    :o-support
    (state <s>    ^name aileen
                  ^world <w>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^<< distance >> <val>)
    (<objs>    -^object <root-obj>)
-->
    (<rels>    ^relation <rel> -)
    }

sp {aileen*elaborate*remove*csrs*no-target-object
    :o-support
    (state <s>    ^name aileen
                  ^world <w>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root-object <root-obj>
              ^target-object <target-obj>
              ^<< distance >> <val>)
    (<objs>    -^object <target-obj>)
-->
    (<rels>    ^relation <rel> -)
    }
      
              
