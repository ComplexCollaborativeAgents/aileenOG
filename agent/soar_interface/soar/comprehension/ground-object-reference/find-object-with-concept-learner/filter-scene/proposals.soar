##!
# @file
#
# @created smohan 20191202

sp {filter-scene*propose*mark-unknown-concept
    (state <s>    ^name filter-scene
                  ^visual-word <tag>
                  ^smem-query <query>
                  ^topstate <ts>)
    (<query>    ^cue <cue>
                ^result failure)
    (<cue>    ^nl <tag>)
-->
    (<s>    ^operator <op> + >)
    (<op>    ^name mark-unknown-visual-concept
             ^nl <tag>
             ^type visual)
   }

sp {filter-scene*propose*elaborate-with-concept
    (state <s>    ^name filter-scene
                  ^visual-word <tag>
                  ^retrieved-concept-lti <res>
                  -^elaborated-with <concept>
                   ^candidate-set <cs>)
    (<res>    ^nl <tag>
              ^gpool <gpool>
              ^concept <concept>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name elaborate-with-concept
             ^gpool <gpool>
             ^candidate-set <cs>
             ^concept <concept>)
    }
              


sp {filter-scene*propose*filter-for-concept
    (state <s>    ^name filter-scene
                  ^visual-word <tag>
                  ^retrieved-concept-lti <res>
                  ^candidate-set <cs>
                  ^elaborated-with <concept>
                  -^filtered-with <concept>)
    (<res>    ^nl <tag>
              ^gpool <gpool>
              ^concept <concept>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name filter-for-concept
             ^gpool <gpool>
             ^candidate-set <cs>
             ^concept <concept>)
    }

sp {filter-scene*propose*complete-filter-scene
    (state <s>    ^name filter-scene
                  ^visual-word <tag>
                  ^retrieved-concept-lti <lti>
                  ^filtered-with <concept>
                  ^elaborated-with <concept>
                  ^candidate-set <cs>)
    (<lti>    ^concept <concept>
              ^nl <tag>)
    (<cs>    ^object)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name complete-filter-scene
             ^tag <tag>)
    }

sp {filter-scene*propose*mark-overspecific-visual-concept
    (state <s>    ^name filter-scene
                  ^visual-word <tag>
                  ^retrieved-concept-lti <res>
                  ^candidate-set <cs>
                  ^elaborated-with <concept>
                  ^filtered-with <concept>)
    (<cs>    -^object)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name mark-overspecific-visual-concept
             ^concept-structure <res>
             ^type visual)
    }


                  
    