##!
# @file
#
# @created smohan 20191202



sp {filter-scene*apply*filter-with-gpool
    (state <s>    ^name filter-scene
                  ^operator <op>
                  ^epmem.present-id <pid>
                  ^io.output-link <ol>)
    (<op>    ^name filter-with-gpool
             ^gpool <gpool>
             ^candidate-set <cs>
             ^concept <concept>)
-->
    (<ol>    ^concept-memory.query <query>)
    (<query>    ^gpool <gpool>
                ^concept <concept>
                ^scene <scene>
                ^context <pid>)
    }

sp {filter-scene*apply*filter-with-gpool*object*id
    (state <s>    ^name filter-scene
                  ^operator <op>
                  ^io.output-link <ol>)
    (<ol>    ^concept-memory.query.scene <scene>)
    (<op>    ^candidate-set.object <cso>)
-->
    (<scene>    ^object <obj>)
    (<s>    ^<cso> <obj>)
    }

sp {filter-scene*apply*filter-with-gpool*object*attrs
    (state <s>    ^name filter-scene
                  ^operator <op>
                  ^io.output-link <ol>
                  ^topstate._params.relevant-percept-set.type <attr>
                  ^<cso> <obj>)
    (<ol>    ^concept-memory.query.scene <scene>)
    (<op>    ^candidate-set.object <cso>)
    (<cso>    ^<attr> <val>)
    (<scene>    ^object <obj>)
-->
    (<obj>    ^<attr> <val>)
    }

sp {filter-scene*apply*filter-with-gpool*object*matches
    (state <s>    ^name filter-scene
                  ^operator <op>
                  ^io.input-link.concept-memory.result <cm>
                  ^candidate-set <cs>)
    (<cs>    ^object <obj>)
    (<obj>    ^id_string <id-name>)
    -{(<cm>    ^matches <m>)
    (<m>    ^id_name <id-name>)}
    (<op>    ^name filter-with-gpool
             ^concept <concept>)
-->
    (<cs>    ^object <obj> -)
    (<s>    ^filtered-with <concept>)
    }

sp {filter-scene*apply*filter-with-gpool*object*mark-status
    (state <s>    ^name filter-scene
                  ^operator <op>
                  ^io.input-link.concept-memory.result <cm>)
    (<op>    ^name filter-with-gpool
             ^concept <concept>)
-->
    (<s>    ^filtered-with <concept>)
    }
                  