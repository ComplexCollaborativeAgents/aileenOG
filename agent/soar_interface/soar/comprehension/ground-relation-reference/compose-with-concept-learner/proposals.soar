##!
# @file
#
# @created smohan 20191217

sp {compose-with-concept-learner*propose*compose-relation-query
    (state <s>    ^name compose-with-concept-learner
                  ^map <res>)
    -{(<s>    ^relation-query <rel>)
      (<rel>    ^lfirst <lf>
                ^lsecond <ls>
                ^lthird <lt>)}
-->
    (<s>    ^operator <op> +)
    (<op>    ^name compose-relation-query
             ^relation <res>)
    }
            
sp {compose-with-concept-learner*propose*collect-relation-facts
    (state <s>    ^name compose-with-concept-learner
                  ^relation-query <query>
                  -^facts.fact)
    (<query>    ^lfirst
                ^lsecond
                ^lthird)
-->
    (<s>    ^operator <op> + >)
    (<op>    ^name collect-relation-facts
             ^relation-query <query>)
    }

sp {compose-with-concept-learner*propose*query-concept-memory
    (state <s>    ^name compose-with-concept-learner
                  ^relation-query <q>
                  ^facts <f>
                  -^relations.relation
                   -^queried-with <q>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name query-concept-memory
             ^facts <f>
             ^query <q>)
    }

sp {compose-with-concept-learner*propose*mark-overspecific-spatial-concept
    (state <s>    ^name compose-with-concept-learner
	              ^relation-query <q>
	              ^queried-with <q>
                  ^map <con>
	             -^relations.relation
                 -^io.input-link.concept-memory.result.matches <m>)
-->
	(<s>    ^operator <op> +)
	(<op>    ^name mark-overspecific-spatial-concept
             ^concept-structure <con>
             ^type spatial)
 }

sp {compose-with-concept-learner*propose*reinforce-correct-query
    (state <s>    ^name compose-with-concept-learner
                  ^topstate <ts>
                  ^relations.relation <rel>
                  ^relation-query <q>
                  ^facts <facts>
                  -^reinforced <q>)
    (<ts>    ^_params.policies.store reinforce-query-inference
             ^interaction.stack.segment <seg>)
    (<seg>    ^purpose.type inform
              -^learned true)

-->
    (<s>    ^operator <op> +)
    (<op>    ^name reinforce-correct-query-inference
             ^query <q>
             ^facts <facts>)
    }

sp {compose-with-concept-learner*propose*complete-compose-with-concept-learner
    (state <s>    ^name compose-with-concept-learner
                  ^relations.relation <rel>)
-->
    (<s>    ^operator <op> + < =)
    (<op>     ^name complete-compose-with-concept-learner
              ^relation <rel>)
    }