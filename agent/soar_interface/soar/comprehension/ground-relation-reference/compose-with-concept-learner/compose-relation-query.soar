##!
# @file
#
# @created smohan 20191211

sp {compose-with-concept-learner*compose-relation-query
    (state <s>    ^name compose-with-concept-learner
                  ^operator <op>)
    (<op>    ^name compose-relation-query
             ^relation <rel>)
    (<rel>    ^concept <con>)
-->
    (<s>    ^relation-query <query>)
    (<query>    ^lfirst <con>)
    }

sp {compose-with-concept-learner*compose-relation-query*objects*first
    (state <s>    ^name compose-with-concept-learner
                  ^operator <op>
                  ^grounding <g>
                  ^parses.temporal <t>
                  ^relation-query <query>)
    (<op>    ^name compose-relation-query)
    (<t>    ^order.first <obj-ref>)
    (<g>    ^entity <en>)
    (<en>    ^_metadata.description <obj-ref>
             ^object.id_string <id>)
-->
    (<query>    ^lsecond <id>)
    }

sp {compose-with-concept-learner*compose-relation-query*objects*second
    (state <s>    ^name compose-with-concept-learner
                  ^operator <op>
                  ^grounding <g>
                  ^parses.temporal <t>
                  ^relation-query <query>)
    (<op>    ^name compose-relation-query)
    (<t>    ^order.second <obj-ref>)
    (<g>    ^entity <en>)
    (<en>    ^_metadata.description <obj-ref>
             ^object.id_string <id>)
-->
    (<query>    ^lthird <id>)
    }