##!
# @file
#
# @created smohan 20191206
sp {aileen*propose*update-world*new*object
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects <wobjs>)
    (<il>    ^world.objects.object <obj>)
   -{(<wobjs>    ^object <wobj>)
    (<wobj>    ^id <id>)}
-->
    (<s>    ^operator <op> +)
    (<op>    ^name update-world
             ^add-object <obj>)
}

sp {aileen*propose*update-world*remove*object
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects <wobjs>)
    -{(<il>    ^world.objects.object <obj>)
    (<obj>    ^id <id>)}
    (<wobjs>    ^object <wobj>)
    (<wobj>    ^id <id>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name update-world
             ^remove-object <wobj>)
    }

sp {aileen*propose*update-world*update*attribute
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world.objects.object <wobj>)
    (<il>    ^world.objects.object <obj>)
    (<obj>    ^id <id>
              ^<attr> <val>)
    (<wobj>    ^id <id>
               ^<attr> {<nval> <> <val>})
-->
    (<s>    ^operator <op> +)
    (<op>    ^name update-world
             ^update-attribute <utr>)
    (<utr>    ^object <wobj>
               ^attribute <attr>
               ^old-value <nval>
               ^new-value <val>)
    }

sp {aileen*propose*update-world*add*relation
    (state <s>    ^name aileen
                  ^io.input-link <il>
                  ^world <w>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<il>    ^world.qsrs.qsr <qsr>)
    (<qsr>    ^root <root-num>
              ^target <target-num>
              ^{<type> << cardir rcc8 >>} <val>)
    (<objs>    ^object <root-obj>
                 ^object <target-obj>)
    (<root-obj>    ^id <root-num>)
    (<target-obj>    ^id <target-num>)
    -{(<rels>    ^relation <any-rel>)
      (<any-rel>    ^root <root-obj>
                ^target <target-obj>
                ^<type> <val>)}
-->
    (<s>    ^operator <op> +)
    (<op>    ^name update-world
             ^add-relation <rel>)
    (<rel>    ^root <root-obj>
              ^target <target-obj>
              ^<type> <val>)
    }

sp {aileen*propose*remove*relation*no-root-object
    (state <s>    ^name aileen
                  ^world <w>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root <root-obj>
              ^target <target-obj>
              ^<< cardir rcc8 >> <val>)
    (<objs>    -^object <root-obj>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name update-world
             ^remove-relation <rel>)
    }

sp {aileen*propose*remove*relation*no-target-object
    :o-support
    (state <s>    ^name aileen
                  ^world <w>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root <root-obj>
              ^target <target-obj>
              ^<< cardir rcc8 >> <val>)
    (<objs>    -^object <target-obj>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name update-world
             ^remove-relation <rel>)
    }

sp {aileen*propose*remove*relation*no-qsr
      (state <s>    ^name aileen
                  ^world <w>
                  ^io.input-link.world.qsrs.qsr <irel>)
    (<w>    ^objects <objs>
            ^relations <rels>)
    (<rels>    ^relation <rel>)
    (<rel>    ^root <root-obj>
              ^target <target-obj>
              ^{<< cardir rcc8 >> <type>} <val>)
    (<objs>    ^object <target-obj> <root-obj>)
    (<irel>    ^root <root-num>
               ^target <target-num>
               ^<type> {<nval> <> <val>})
    (<root-obj>    ^id <root-num>)
    (<target-obj>    ^id <target-num>)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name update-world
             ^remove-relation <rel>)
    }
             
    
             